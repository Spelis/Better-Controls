buildscript {
	repositories {
		maven { url = 'https://files.minecraftforge.net/maven' }
		maven { url = "https://repo.spongepowered.org/maven" }
		mavenCentral()
	}
	
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
		classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
	}
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'

def mcversion = "1.17.1"
def forgeversion = "37.0.75"

def prefixName = 'displayName = '
def prefixVersion = 'version = '

def metaLines = file('src/main/resources/META-INF/mods.toml').readLines()
def metaName = metaLines.find { line -> line.startsWith(prefixName) }.substring(prefixName.length())[1..-2]
def metaVersion = metaLines.find { line -> line.startsWith(prefixVersion) }.substring(prefixVersion.length())[1..-2]

group = 'chylex.bettercontrols'
version = metaVersion
archivesBaseName = metaName.replaceAll('\\s', '')

java.toolchain.languageVersion = JavaLanguageVersion.of(16)

minecraft {
	mappings channel: 'official', version: '1.17.1'
	
	runs {
		client {
			workingDirectory file('run')
			
			property 'mixin.env.disableRefMap', 'true'
			arg '-mixin.config=bettercontrols.mixins.json'
			
			mods {
				bettercontrols {
					source sourceSets.main
				}
			}
		}
		
		server {
			workingDirectory file('run')
			
			property 'mixin.env.disableRefMap', 'true'
			arg '-mixin.config=bettercontrols.mixins.json'
			
			mods {
				bettercontrols {
					source sourceSets.main
				}
			}
		}
	}
}

dependencies {
	minecraft 'net.minecraftforge:forge:' + mcversion + '-' + forgeversion
}

jar {
	archiveName = archivesBaseName + '-' + mcversion + '+v' + version + '.jar'
	
	from('./') {
		include 'LICENSE'
	}
	
	manifest {
		attributes([
			'Specification-Title'     : 'bettercontrols',
			'Specification-Version'   : '1',
			'Specification-Vendor'    : 'chylex',
			
			'Implementation-Title'    : metaName,
			'Implementation-Version'  : metaVersion,
			'Implementation-Vendor'   : 'chylex',
			
			'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
			
			'MixinConfigs'            : 'bettercontrols.mixins.json'
		])
	}
	
	finalizedBy('reobfJar')
}
